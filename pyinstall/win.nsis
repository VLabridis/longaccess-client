;--------------------------------
;Include Modern UI
!include x64.nsh
!include "MUI2.nsh"

;--------------------------------
;General

;Name and file
Name "Longaccess CLI"
OutFile "Longaccess-0.1a2-Win7-64bit.exe"

;Default installation folder
InstallDir "$PROGRAMFILES64\LongaccessCLI\"

;Get installation folder from registry if available
InstallDirRegKey HKCU "Software\LongaccessCLI" ""

;Request application privileges for Windows Vista
RequestExecutionLevel admin

;--------------------------------
;Interface Settings

!define MUI_ABORTWARNING

;--------------------------------
;Pages

!insertmacro MUI_PAGE_LICENSE "..\LICENSE"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

;--------------------------------
;Languages

!insertmacro MUI_LANGUAGE "English"

Var Extension
Var ExtCmd
Function un.setExtensionVars

StrCpy $Extension ".longaccess"
StrCpy $ExtCmd '"$INSTDIR\lacli\lacli.exe" archive extract'
FunctionEnd
Function setExtensionVars

StrCpy $Extension ".longaccess"
StrCpy $ExtCmd '"$INSTDIR\lacli\lacli.exe" archive extract'
FunctionEnd

;--------------------------------
;Installer Sections

Section "Install"
SetRegView 64


SetOutPath "$INSTDIR"
Call setExtensionVars
File /r "dist\lacli"


WriteRegStr HKCU "Software\Classes\lacli" "DefaultIcon" "$INSTDIR\lacli\lacli.exe"
WriteRegStr HKCU "Software\Classes\lacli\shell\open" "" "Open with Long Access CLI"
WriteRegStr HKCU "Software\Classes\lacli\shell\open\command" "" '$ExtCmd "%1"'

WriteRegStr HKCU "Software\LongaccessCLI" "" $INSTDIR
WriteRegStr HKCU "Software\LongaccessCLI\Capabilities" "ApplicationDescription" "the Longaccess CLI client"
WriteRegStr HKCU "Software\LongaccessCLI\Capabilities" "ApplicationName" "lacli"
WriteRegStr HKCU "Software\LongaccessCLI\Capabilities\FileAssociations" $Extension "lacli"

WriteRegStr HKCU "Software\RegisteredApplications" "lacli" "Software\LongaccessCLI\Capabilities"


WriteRegStr HKCU "Software\Classes\.longaccess\OpenWithProgids" "lacli" ""

;Create uninstaller
WriteUninstaller "$INSTDIR\Uninstall.exe"

;Create shortcuts
CreateDirectory $SMPROGRAMS\LongaccessCLI
CreateShortCut "$SMPROGRAMS\LongaccessCLI\LongaccessCLI.lnk" "$INSTDIR\lacli\lacli.exe" "-i" ; use defaults for parameters, icon, etc.
CreateShortCut "$SMPROGRAMS\LongaccessCLI\Uninstall Longaccess.lnk" "$INSTDIR\Uninstall.exe" ; use defaults for parameters, icon, etc.

SectionEnd


;--------------------------------
;Uninstaller Section

Section "Uninstall"
SetRegView 64

Call un.setExtensionVars

Delete "$INSTDIR\Uninstall.exe"
RMDir /r "$INSTDIR"
Delete "$SMPROGRAMS\LongaccessCLI\Longaccess.lnk"
Delete "$SMPROGRAMS\LongaccessCLI\Uninstall Longaccess.lnk"

DeleteRegKey HKCU "Software\Classes\lacli"
DeleteRegValue HKCU "Software\RegisteredApplications" "lacli"
DeleteRegValue HKCU "Software\Classes\.longaccess\OpenWithProgids" "lacli"
DeleteRegKey HKCU "Software\LongaccessCLI"

SectionEnd

